S -> Program 'EOF',
Program -> ClassDeclaration*,
ClassDeclaration -> 'Class' 'IDENT' 'LeftCurlyBracket' ClassMember* 'RightCurlyBracket',
ClassMember -> Field | Method | MainMethod,
Field -> 'Public' Type 'IDENT' 'Semicolon',
MainMethod -> 'Public' 'Static' 'Void' 'IDENT' 'LeftParen' Type 'IDENT' 'RightParen' MethodRest? Block,
Method -> 'Public' Type 'IDENT' 'LeftParen' Parameters? 'RightParen' MethodRest? Block,
MethodRest -> 'Throws' 'IDENT',
Parameters -> Parameter ( 'Comma' Parameters)*,
Parameter -> Type 'IDENT',
Type -> Type 'LeftSquareBracket' 'RightSquareBracket' | BasicType,
BasicType -> 'Int' | 'Boolean' | 'Void' | 'IDENT',
Statement -> Block
           | EmptyStatement
           | IfStatement
           | ExpressionStatement
           | WhileStatement
           | ReturnStatement,

Block -> 'LeftCurlyBracket' BlockStatement* 'RightCurlyBracket',
BlockStatement -> Statement | LocalVariableDeclarationStatement,
LocalVariableDeclarationStatement -> Type 'IDENT' ( 'Assign' Expression )? 'Semicolon',
EmptyStatement -> 'Semicolon',
WhileStatement -> 'While' 'LeftParen' Expression 'RightParen' Statement,
IfStatement -> 'If' 'LeftParen' Expression 'RightParen' Statement ( 'Else' Statement )?,
ExpressionStatement -> Expression 'Semicolon',
ReturnStatement -> 'Return' Expression? 'Semicolon',
Expression -> AssignmentExpression,
AssignmentExpression -> LogicalOrExpression ('Assign' AssignmentExpression)?,
LogicalOrExpression -> LogicalAndExpression ('Or' LogicalAndExpression)*,
LogicalAndExpression -> EqualityExpression ('And' EqualityExpression)*,
EqualityExpression -> RelationalExpression (('Equals' | 'NotEquals') RelationalExpression)*,
RelationalExpression -> AdditiveExpression (('LessThan' | 'LessThanOrEquals' | 'GreaterThan' | 'GreaterThanOrEquals') AdditiveExpression)*,
AdditiveExpression -> MultiplicativeExpression (('Add' | 'Subtract') MultiplicativeExpression)*,
MultiplicativeExpression -> UnaryExpression (('Multiply' | 'Divide' | 'Modulo') UnaryExpression)*,
UnaryExpression -> PostfixExpression | ('Not' | 'Subtract') UnaryExpression,
PostfixExpression -> PrimaryExpression (PostfixOp)*,
PostfixOp -> MethodInvocation
           | FieldAccess
           | ArrayAccess,

MethodInvocation -> 'Dot' 'IDENT' 'LeftParen' Arguments 'RightParen',
FieldAccess -> 'Dot' 'IDENT',
ArrayAccess -> 'LeftSquareBracket' Expression 'RightSquareBracket',
Arguments -> (Expression ('Comma' Expression)*)?,
PrimaryExpression -> 'Null' | 'False' | 'True' | 'INTEGER_LITERAL' | 'IDENT' | 'IDENT' 'LeftParen' Arguments 'RightParen' | 'This'
                    | 'LeftParen' Expression 'RightParen' | NewObjectExpression | NewArrayExpression,

NewObjectExpression -> 'New' 'IDENT' 'LeftParen' 'RightParen',
NewArrayExpression -> 'New' BasicType 'LeftSquareBracket' Expression 'RightSquareBracket' ( 'LeftSquareBracket' 'RightSquareBracket')*
